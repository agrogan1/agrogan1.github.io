% Can Stata Make A Beautiful Graph?
% Andy Grogan-Kaylor 
% `s c(current_date)` `s c(current_time)`

---
geometry: margin=1 in
---

<style>body {max-width: 90%} h1 {color: #00274C;} h2 {color: #2F65A7;} p{font-size: 115%} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;} code {color: red;}</style>

# Introduction ðŸŒ²

There is sometimes a *debate*--shall we say--about whether Stata can make beautiful graphs, particularly in comparison to using `ggplot` in R. Below, I try to use as many of Stata's options as I can to make a beautiful graph. I'll admit at the outset that it is perhaps not as beautiful as the same graph would be in `ggplot`, but believe that it comes close.

I use green and gold since they are common colors for forestry and park signage.

# Data Source ðŸŒ²

The data used in this example are derived from the R package *Functions and Datasets for "Forest Analytics with R"*.

According to the documentation, the source of these data are: "von Guttenbergâ€™s Norway spruce (Picea abies [L.] Karst) tree measurement data."

![Old Tjikko, a 9,550 Year Old Norway Spruce in Sweden](Old-Tjikko-Small.jpg)

The documentation goes on to further note that:

> "The data are measures from 107 trees. The trees were selected as being of average size from healthy and well stocked stands in the Alps."

    use "https://github.com/agrogan1/multilevel/raw/master/mlm-R2-gutten/gutten.dta", clear

# A Simple Graph ðŸŒ²

    twoway (scatter height age_base)(lowess height age_base)
	
	graph export gutten0.svg, width(2000) replace

![A Simple Graph of the Gutten Trees](gutten0.svg){width=25%}

# A Beautiful Graph ðŸŒ²

Below please find an *admittedly complicated* graph command. Note, however, that the graph builds upon basic ideas that we want a `twoway` graph, with a `scatter`plot and a `lowess` smoother. Everything else is just an option to improve the appearance of the graph.

I make extensive use of the `///` notation which allows me to spread the graph along multiple lines.

    twoway /// two way graph
    (scatter height age_base, /// scatterplot
	mcolor(dkgreen%25) msymbol(circle)) /// dark green transparent markers
    (lowess height age_base, /// smoother 
	lcolor(gold) lwidth(thick)), /// gold line
    scheme(michigan) /// better base scheme
    title("Tree Height by Tree Age") /// title
    xtitle("Tree Age") /// title for x axis
    ytitle("Tree Height") /// title for y axis
    caption("Gutten Tree Data") /// caption
    legend(order(1 "trees" 2 "smoother") position(6) rows(1)) /// better labels and format for legend
	aspectratio(.75) // better aspect ratio

    graph export gutten.png, width(2000) replace
	
	graph export gutten.svg, width(2000) replace

![A Beautiful? Graph of the Gutten Trees](gutten.svg){width=75%}

	
	
	
	

	
	
	