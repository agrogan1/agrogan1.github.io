% Why Excel Is A Bad Format For Storing Data
% Andy Grogan-Kaylor 
% `s c(current_date)` `s c(current_time)`

---
geometry: margin=1 in
---

<style>body {max-width: 90%;} h1 {color: #00274C;} h2 {color: #2F65A7;} blockquote {border-left: 5px solid #ffcb05; margin: 1.5em 10px; padding: 0.5em 10px;} code {color: red;}</style>

```{s/}
    * setup

    clear all
	
	set seed 3846 // random seed for data

    * simulate data so that we have some data to work with

    quietly set obs 100 // 100 observations
	
	generate id = _n // id variable
	
	label variable id "id"

    generate Q1 = runiformint(1,4) // randomly simulated Question 1

    label variable Q1 "How often do you feel happy?" // add variable label

    label define Likert1 1 "Never" 2 "Rarely" 3 "Sometimes" 4 "Often" // define value label
	
    label values Q1 Likert1 // add value label to Q1
	
	quietly save "simulated-happiness-data.dta", replace
```

# Get The Data

```{s}
    use "simulated-happiness-data.dta", clear
```
	
# Describe The Data

```{s}
    describe
```
	
# Descriptive Statistics and Bar Graph

> Notice how the descriptive statistics and graph are informative in that they contain information on the *variable label* and *value label*. These help us to get an intuitive sense of the information in the data. We see this information when we list out the data as well.

## Descriptive Statistics

```{s}
	tabulate Q1
```

## Bar Graph

```{s}
    graph bar, over(Q1) scheme(michigan2) asyvars
	
    graph export mybar1.png, width(500) replace
```

![Bar Graph](mybar1.png)

## List Out A Sample Of The Data

```{s}
    list in 1/10
```

# Now Save The Data In Excel Format

We've saved this simulated data in Excel format, and now import the Excel data file. We use the first row of data as variable names.

```{s/}
    quietly export excel using "simulated-happiness-data.xls", firstrow(variables) nolabel replace
```

```{s}
    import excel "simulated-happiness-data.xls", sheet("Sheet1") firstrow clear
```
	
# Descriptive Statistics and Bar Graph

> Notice how the descriptive statistics and graph are much less informative. For example, it is now not immediately clear what `Q1` represents. 

> It is also not clear whether higher values of `Q1` represent higher levels of *happiness*, or higher levels of *unhappiness*, a crucially important substantive distinction. The information on variable label and value label will have to be added back into the data when preparing a report for dissemination.

## Descriptive Statistics

```{s}
	tabulate Q1
```

### Bar Graph

```{s}
    graph bar, over(Q1) scheme(michigan2) asyvars
	
    graph export mybar2.png, width(500) replace
```

![Bar Graph](mybar2.png)

# A Few Final Issues

Notice how Excel doesn't enforce the idea of whether variables are *numeric*, or *string*, and so would allow storage of different types of information in the same column. 

Secondly, Excel would allow some of your columns to have the same name, which might make data difficult to work with in other software.

| x           | y           | verylongvariablename | verylongvariablename |
| ----------- | ----------- |--------------------- | -------------------- |
| 100         | 1           | Smith                | 20                   |
| 200         | 2           | 30                   | NA                   |
| not applicable   | x      | yes                  | 60                   |














